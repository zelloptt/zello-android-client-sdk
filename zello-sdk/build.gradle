buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
    }
}

apply plugin: 'com.android.library'

dependencies {
    compile fileTree(include: '*.jar', dir: 'libs')
}

task zelloSdkJarCreate(type: Copy) {
    description "Creates SDK jar in the project root folder"
    delete 'libs/zello-sdk.jar'
    from('build/intermediates/bundles/release/')
    into('../')
    include('classes.jar')
    rename('classes.jar', 'zello-sdk.jar')
}

android.libraryVariants.all { variant ->
    if (variant.name.equals('release')) {
        task("zelloSdkDocCreate", type: Javadoc) {
            title ""
            description "Creates Zello SDK Javadoc in the project root folder"
            destinationDir = new File("${project.getProjectDir()}/../docs", "zello-sdk-documentation")
            source = android.sourceSets.main.java.srcDirs
            ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
            println("${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar")
            classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
            exclude '**/Constants.java'
            exclude '**/ContactObserver*.java'
            options.memberLevel = JavadocMemberLevel.PUBLIC
            options.noTree = false
            options.noNavBar = false
            options.noIndex = false
            options.splitIndex = false
            options.use = false
            options.version = false
            options.author = false
            options.noDeprecated = true
            options.noDeprecatedList = true
            options.stylesheetFile = new File(projectDir, "../docs/stylesheet.css");
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease') {
        task.finalizedBy 'zelloSdkJarCreate'
        task.finalizedBy 'zelloSdkDocCreate'
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion '25.0.2'

    defaultConfig {
        targetSdkVersion 23
        minSdkVersion 9
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            //resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        //instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}
